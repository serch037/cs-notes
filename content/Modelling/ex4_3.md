Title: Fourth Exercise  
Date: 2015-09-09
Tags: Fifth-Semester
Summary: This program spawns particles around the mouse

## Experience
The program required some simple but interesting modifications from the provided example, I had to reconsider my choice once I realized my original plan of creating multiple particle systems was less efficient as changing the coordinates of the origin of individual particles. 

## Video 
https://drive.google.com/open?id=0B7aIgTzs5Ai_MmZsODZlUDV5eHM

## Code
:::java
    // The Nature of Code
    // Daniel Shiffman
    // http://natureofcode.com

    ParticleSystem ps;

    void setup() {
      size(640,360);
      ps = new ParticleSystem(new PVector(mouseX, mouseY));
    }

    void draw() {

      background(255);

      ps.addParticle();
      ps.run();
    }
    // The Nature of Code
    // Daniel Shiffman
    // http://natureofcode.com

    // Simple Particle System

    class Particle {
      PVector location;
      PVector velocity;
      PVector acceleration;
      float lifespan;

      Particle(PVector l) {
        acceleration = new PVector(0,0.05);
        velocity = new PVector(random(-1,1),random(-2,0));
        location = l.get();
        lifespan = 255.0;
      }

      void run() {
        update();
        display();
      }

      // Method to update location
      void update() {
        velocity.add(acceleration);
        location.add(velocity);
        lifespan -= 2.0;
      }

      // Method to display
      void display() {
        stroke(0,lifespan);
        strokeWeight(2);
        fill(127,lifespan);
        ellipse(location.x,location.y,12,12);
      }
      
      // Is the particle still useful?
      boolean isDead() {
        if (lifespan < 0.0) {
          return true;
        } else {
          return false;
        }
      }
    }
    // The Nature of Code
    // Daniel Shiffman
    // http://natureofcode.com

    // Using Generics now!  comment and annotate, etc.

    class ParticleSystem {
      ArrayList<Particle> particles;
      PVector origin;

      ParticleSystem(PVector location) {
        origin = location.get();
        particles = new ArrayList<Particle>();
      }

      void addParticle() {
        particles.add(new Particle(new PVector(mouseX,mouseY)));
      }

      void run() {
        for (int i = particles.size()-1; i >= 0; i--) {
          Particle p = particles.get(i);
          p.run();
          if (p.isDead()) {
            particles.remove(i);
          }
        }
      }
    }
